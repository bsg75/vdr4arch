# This PKGBUILD is part of the VDR4Arch project [https://github.com/vdr4arch]

# Maintainer: Christopher Reimer <mail+vdr4arch[at]c-reimer[dot]de>
pkgname=vdr-radio
pkgver=AAC_RDSv10.r0.gf0c89c2
_gitver=f0c89c2c2285743bcc62ecca473b270359af8775
_vdrapi=2.6.3
pkgrel=1
pkgdesc="Display an background image while the vdr is switched to radio channels"
url="git+https://github.com/siricco/vdr-plugin-radio.git#commit=$_gitver"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
license=('GPL2')
depends=('gcc-libs' "vdr-api=${_vdrapi}")
_plugname=${pkgname//vdr-/}
source=("git+https://github.com/siricco/vdr-plugin-radio.git#commit=$_gitver"
        "50-$_plugname.conf")
backup=("etc/vdr/conf.avail/50-$_plugname.conf")
sha256sums=('SKIP'
            'f4d789b518e9343fd554035f494157b4f8970ec653b2849260e0a442bcd8ee64')

pkgver() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
#  cd "$pkgname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  make
}

package() {
  cd "${srcdir}/vdr-plugin-${_plugname}"
  make DESTDIR="${pkgdir}" install

  #Install all stillpictures
  mkdir -p "$pkgdir/usr/share/vdr/plugins/$_plugname"
  install -Dm644 config/mpegstill/* "$pkgdir/usr/share/vdr/plugins/$_plugname"

  install -Dm644 "$srcdir/50-$_plugname.conf" "$pkgdir/etc/vdr/conf.avail/50-$_plugname.conf"
}

